# textediting
<n> := 0..99;

# Movement commands
New line below = {End}{End}{Enter};

<spellingcharacters> := 
	( alpha=a | bravo=b | charlie=c | delta=d | echo=e | foxtrot=f
	| golf=g | hotel=h | india=i | juliett=j | kilo=k | lima=l
	| mike=m | november=n | oscar=o | papa=p | quebec=q | romeo=r
	| sierra=s | tango=t | uniform=u | victor=v | whiskey=w
	| xray=x | yankee=y | zulu=z
	| Cap alpha=A | Cap bravo=B | Cap charlie=C | Cap delta=D 
	| Cap echo=E | Cap foxtrot=F | Cap golf=G | Cap hotel=H 
	| Cap india=I | Cap juliett=J | Cap kilo=K | Cap lima=L 
	| Cap mike=M | Cap november=N | Cap oscar=O | Cap papa=P 
	| Cap quebec=Q | Cap romeo=R | Cap sierra=S | Cap tango=T 
	| Cap uniform=U | Cap victor=V | Cap whiskey=W
	| Cap xray=X | Cap yankee=Y | Cap zulu=Z
	| A=a | B=b | C=c | D=d | E=e | F=f | G=g | H=h | I=i | J=j | K=k | L=l 
	| M=m | N=n | O=o | P=p | Q=q | R=r | S=s | T=t | U=u | V=v | W=w | X=x 
	| Y=y | Z=z
	| cap A=A | cap B=B | cap C=C | cap D=D | cap E=E 
	| cap F=F | cap G=G | cap H=H | cap I=I | cap J=J 
	| cap K=K | cap L=L | cap M=M | cap N=N | cap O=O 
	| cap P=P | cap Q=Q | cap R=R | cap S=S | cap T=T 
	| cap U=U | cap V=V | cap W=W | cap X=X | cap Y=Y | cap Z=Z
	| 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
	| "!" | "@" | "#" | "$" | "%" | "^" | "&" | "*" | "(" | ")" | "`" 
	| "~" | "-" | "_" | "=" | "+" | "\" | "|" | "[" | "{" | "]" | "}" 
	| ";" | ":" | "'" | '"' | "," | "<" | "." | ">" | "/" | "?"
	# Aliases
	| Minus="-" | Hyphen="-"
	| dot="." | percent="%" | vertical="|" | vertical pipe="|" | dollar="$"
	| hash="#" | pound="#" | sharp="#" | octothorp="#"
	| less than="<" | greater than=">" | left angle="<" | right angle=">"
	| equals="="
	| Proper ellipsis="…" | m dash="—" | n dash="–"
	);
	
<special> := 
	( Left | Right | Up | Down | space=" " | tab-key=Tab | Enter
	| page-up=PgUp | page-down=PgDn | Backspace | delete=Del
	| escape=Esc | Home | End
	);

<key> := 
	( alpha=a | bravo=b | charlie=c | delta=d | echo=e | foxtrot=f
	| golf=g | hotel=h | india=i | juliett=j | kilo=k | lima=l
	| mike=m | november=n | oscar=o | papa=p | quebec=q | romeo=r
	| sierra=s | tango=t | uniform=u | victor=v | whiskey=w
	| xray=x | yankee=y | zulu=z
	| Cap alpha=A | Cap bravo=B | Cap charlie=C | Cap delta=D 
	| Cap echo=E | Cap foxtrot=F | Cap golf=G | Cap hotel=H 
	| Cap india=I | Cap juliett=J | Cap kilo=K | Cap lima=L 
	| Cap mike=M | Cap november=N | Cap oscar=O | Cap papa=P 
	| Cap quebec=Q | Cap romeo=R | Cap sierra=S | Cap tango=T 
	| Cap uniform=U | Cap victor=V | Cap whiskey=W
	| Cap xray=X | Cap yankee=Y | Cap zulu=Z
	| A=a | B=b | C=c | D=d | E=e | F=f | G=g | H=h | I=i | J=j | K=k | L=l 
	| M=m | N=n | O=o | P=p | Q=q | R=r | S=s | T=t | U=u | V=v | W=w | X=x 
	| Y=y | Z=z
	| cap A=A | cap B=B | cap C=C | cap D=D | cap E=E 
	| cap F=F | cap G=G | cap H=H | cap I=I | cap J=J 
	| cap K=K | cap L=L | cap M=M | cap N=N | cap O=O 
	| cap P=P | cap Q=Q | cap R=R | cap S=S | cap T=T 
	| cap U=U | cap V=V | cap W=W | cap X=X | cap Y=Y | cap Z=Z
	| 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9
	| "!" | "@" | "#" | "$" | "%" | "^" | "&" | "*" | "(" | ")" | "`" 
	| "~" | "-" | "_" | "=" | "+" | "\" | "|" | "[" | "{" | "]" | "}" 
	| ";" | ":" | "'" | '"' | "," | "<" | "." | ">" | "/" | "?"
	| Left | Right | Up | Down | space=" " | tab-key=Tab | Enter
	| page-up=PgUp | page-down=PgDn | Backspace | delete=Del
	| Escape=Esc | Home | End
	# Aliases
	| Minus="-" | Hyphen="-"
	| dot="." | percent="%" | vertical="|" | vertical pipe="|" | dollar="$"
	| hash="#" | pound="#" | sharp="#" | octothorp="#"
	| less than="<" | greater than=">" | left angle="<" | right angle=">"
	| equals="="
	);

<modifier> := Shift | control=Ctrl | Alt | Win;
<nn> := 1..99;

<key> = {$1};
<special> <nn> = {$1_$2};
[Press] <modifier> <key> <nn> = {$1+$2_$3};
[Press] <modifier> <modifier> <key> <nn> = {$1+$2+$3_$4};
[Press] <modifier> <modifier> <modifier> <key> <nn> = {$1+$2+$3+$4_$5};

#<spellingcharacters> = $1;		 
#<spellingcharacters> <spellingcharacters> = $1$2;
#<spellingcharacters> <spellingcharacters> <spellingcharacters> = $1$2$3;
<spellingcharacters> [<spellingcharacters> [<spellingcharacters> [<spellingcharacters> [<spellingcharacters>]]]] = $1$2$3$4$5;
Fat <spellingcharacters> = " " $1 " ";

# Spelling
Silly <spellingcharacters> [<spellingcharacters> [<spellingcharacters> [<spellingcharacters> [<spellingcharacters>]]]] = $1$2$3$4$5;

# for delimiters
parentheses = "()"{Left};
left paren = "(";
right paren = ")";
[square] brackets = "[]"{Left};
[curly] braces = "{}"{Left};
Angle brackets = "<>"{Left};
[double] quotes = """"""{Left};
single quotes = "''"{Left};
octothorps = "##"{Left};
backticks = "``"{Left};

# Combinations
Ellipsis = "...";
Double colon = "::";

# Turn words into variables
# To edit, open file C:\NatLink\NatLink\Vocola\extensions\vocola_ext_bv.py
# When editing this, say "Load [Voice] Extensions" to reload
Snake case <_anything> = BV.to_underscore_case($1);
Camel case <_anything> = BV.to_camel_case($1);

# ---------------------------------------------------------------------------
# Text Editing

<direction>  := Left | Right | Up | Down;
<left_right> := Left | Right;
<start_end> := (Start={Home} | End={End});

### Characters
<n> <direction>       = {$2_$1};
Kill (Char | 1 | One) = {Del};
Kill Back [1]         = {Backspace};
Kill <n>              = {Del_$1};
[Kill] Back <n>       = {Backspace_$1};

## Words
[One] Word <left_right>= {Ctrl+$1};
<n> Words <left_right> = {Ctrl+$2_$1};
Kill Word              = {Right_2}{Ctrl+Left}{Shift+Ctrl+Right}   {Del};
Kill <n> Words         = {Right_2}{Ctrl+Left}{Shift+Ctrl+Right_$1}{Del};
Kill Back Word         = {Left}{Ctrl+Right}{Shift+Ctrl+Left}   {Del};
Kill Back <n> Words    = {Left}{Ctrl+Right}{Shift+Ctrl+Left_$1}{Del};

### Lines
Line <start_end>     = $1;
New Line             = {Enter};
Line Here            = {Enter}{Left};
Copy Line            = {home}{Shift+Down}{Shift+Home}{Ctrl+c};
Kill Line            = {home}{Shift+Down}{Shift+Home}{Del};
Kill Back Line       = {home}{Shift+Up}  {Shift+Home}{Del};
Kill <n> Lines       = {home}{Shift+Down_$1}{Shift+Home}{Del};
Kill Back <n> Lines  = {home}{Shift+Up_$1}  {Shift+Home}{Del};
Kill Here            = {Shift+End}{Del};
Kill Back Here       = {Shift+Home}{Del};
Duplicate Line       = {home}{Shift+Down}{Shift+Home}{Ctrl+c}{Home}{Ctrl+v};

### Entire "Flow"   
Flow Start           = {Ctrl+Home};
Flow End             = {Ctrl+End};
Select All           = {Ctrl+a};
Copy All             = {Ctrl+a}{Ctrl+c};
(Cut|Kill) All       = {Ctrl+a}{Ctrl+x};
Kill Flow Here       = {Ctrl+Shift+End} {Ctrl+x};
Kill Back Flow Here  = {Ctrl+Shift+Home}{Ctrl+x};
Replace All          = {Ctrl+a}{Del}{Ctrl+v};
                    
### Selection         
Kill That            = {Del};
Cut That             = {Ctrl+x};
Copy That            = {Ctrl+c};
Paste That            = {Ctrl+v};
Paste Here           = ButtonClick() {Ctrl+v};
Duplicate That       = {Ctrl+c}{Left}{Ctrl+v};
Keep That            = {Ctrl+c}{Ctrl+a}{Del}{Ctrl+v};

### Miscellaneous
Undo <n> = {Ctrl+z_$1};
Camel [Case] That = HeardWord(\Cap,That) HeardWord(compound,that) {Ctrl+Left}
                    {Shift+Right} HeardWord(\No-Caps,That){Ctrl+Right};
(Cap | Up Case) <n> = {Shift+Right_$2} HeardWord(\All-Caps,That);

